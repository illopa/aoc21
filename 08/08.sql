create or replace package AOC21_P08 as

    function sort_string(pIn in varchar2) return varchar2;
    function str2digit(pIn in varchar2) return varchar2;

    procedure part_one(pOut in out varchar2);
    procedure part_two(pOut in out varchar2);

end;
/
create or replace package body AOC21_P08 as

    /*

        -- DDL
        
        CREATE TABLE  "AOC21_D08_INPUT" 
        (	"ID" NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY, 
            "COL001" VARCHAR2(255), 
            "COL002" VARCHAR2(50), 
            PRIMARY KEY ("ID")
        USING INDEX  ENABLE
        )

        CREATE TABLE AOC21_D08_PATTERN AS
            SELECT t.ID, rownum rnum, length(st.column_value) len, AOC21_P08.sort_string(st.column_value) pattern
                FROM AOC21_D08_INPUT t,
                TABLE(apex_string.split(t.col001,' ')) st

     */

    function sort_string(pIn in varchar2) return varchar2
    is
        vOut varchar2(1000);
    begin
        Select listagg(column_value,'') WITHIN GROUP (ORDER BY column_value) sorted
            into vOut
            from
            (
                select *
                    from table(apex_string.split(pIn,''))
            );
        return vOut;
    end;

    function str2digit(pIn in varchar2) return varchar2
    is
        vOut varchar2(1);
    begin
        vOut := (case
            when pIn = 'abcefg' then '0'
            when pIn = 'cf' then '1'
            when pIn = 'acdeg' then '2'
            when pIn = 'acdfg' then '3'
            when pIn = 'bcdf' then '4'
            when pIn = 'abdfg' then '5'
            when pIn = 'abdefg' then '6'
            when pIn = 'acf' then '7'
            when pIn = 'abcdefg' then '8'
            else '9'
        end);
        return vOut;
    end;

    procedure part_one(pOut in out varchar2)
    is
    begin
        select count(*) num 
            into pOut
            from (
                select LENGTH(trim(column_value)) val 
                    from AOC21_D08_INPUT i,
                    Apex_string.split(i.col002,' ')
                ) 
            where val IN (2,3,4,7);
    end;

    procedure part_two(pOut in out varchar2)
    is
        vSum number := 0;
        vNum number;

        vA varchar2(1);
        vB varchar2(1);
        vC varchar2(1);
        vD varchar2(1);
        vE varchar2(1);
        vF varchar2(1);
        vG varchar2(1);

        v2L varchar2(10);
        v3L varchar2(10);

        vO3 varchar2(10); 
        vO4 varchar2(10); 
        vO5 varchar2(10); 
        vO6 varchar2(10);

        vPattern varchar2(10);
    begin
        
        FOR cI IN (SELECT ID FROM AOC21_D08_INPUT /*WHERE ID = 1 */)
        LOOP

            Select Listagg(CF,''), Listagg(ACF,'')
            into v2L,v3L
            From (
                Select *
                From AOC21_D08_PATTERN
                where ID = cI.ID
            )
            Pivot (
                Listagg(pattern,'')
                For len in ( 2 CF, 3 ACF)
            );

            -- htp.prn('v2L='||v2L||'<br />');
            -- htp.prn('v3L='||v3L||'<br />');

            vA := TRANSLATE(v3L,'@'||v2L,'@');
            -- htp.prn('vA='||vA||'<br />');

            select o3, o4, o5, o6 
                into vO3, vO4, vO5, vO6 
                from (
                    select occ, lower(listagg(let,''))  str
                    from (
                        select * FROM (
                            select column_value
                            FROM (
                                Select PATTERN
                                From AOC21_D08_PATTERN
                                where ID = cI.ID
                                    AND LEN >= 5 AND LEN <= 6
                            ) t,
                            TABLE(APEX_STRING.split(t.pattern,''))
                        )
                        PIVOT (count(*) FOR column_value IN ('a' a,'b' b,'c' c,'d' d,'e' e,'f' f,'g' g))
                    )      
                    UNPIVOT (
                        occ
                        FOR LET IN (
                            a, b, c, d, e, f, g
                        )
                    )
                    group by occ
                ) 
                PIVOT ( listagg(str,'') FOR occ IN (3 as o3,4 as o4,5 as o5,6 as o6) );

            -- htp.prn('vO3='||vO3||'<br />');
            -- htp.prn('vO4='||vO4||'<br />');
            -- htp.prn('vO5='||vO5||'<br />');
            -- htp.prn('vO6='||vO6||'<br />');


            vG := TRANSLATE(vO6,'@'||vA,'@');
            vE := vO3;

            select column_value
                into vC
                FROM (
                    select * from table(apex_string.split(v2L,''))
                    intersect 
                    select * from table(apex_string.split(vO4,''))
                );

            vF := TRANSLATE(v2L,'@'||vC,'@');
            vD := TRANSLATE(vO5,'@'||vF,'@');
            vB := TRANSLATE(vO4,'@'||vC,'@');

            vPattern := vA || vB || vC || vD || vE || vF || vG;

            -- htp.prn('vPattern='||vPattern||'<br />');

            SELECT listagg(digit,'') WITHIN GROUP (ORDER BY rnum)
                into vNum
                FROM (
                SELECT rownum rnum, AOC21_P08.str2digit(AOC21_P08.sort_string(TRANSLATE(column_value, '@'||vPattern, '@abcdefg'))) digit
                    FROM AOC21_D08_INPUT t,
                    TABLE(apex_string.split(t.col002,' ')) st
                    where t.ID = cI.ID
                );

            vSum := vSum + vNum;

        END LOOP;

        pOut := vSum;

    end;    

end;
/
declare
    vOut varchar2(100);
begin
    AOC21_P08.part_one(pOut => vOut);
    htp.prn('part one: illopa says '||vOut||'<BR />');

    AOC21_P08.part_two(pOut => vOut);
    htp.prn('part two: illopa says '||vOut||'<BR />');
end;